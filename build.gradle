apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'es.danirod'
archivesBaseName = 'gradle-propgen-plugin'
version = '0.1.0'

task wrapper(type: Wrapper) {
  gradleVersion = '2.8'
}

dependencies {
  compile gradleApi()
  compile localGroovy()
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from groovydoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar, sourcesJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  configuration = configurations.archives
  repositories.mavenDeployer {
    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
      authentication(userName: sonatypeUsername, password: sonatypePassword)
    }

    pom.project {
      name archivesBaseName
      packaging 'jar'
      description 'Plugin for generating Java Properties file from Gradle'
      url 'https://github.com/danirod/gradle-propgen-plugin'

      scm {
        connection 'scm:git:https://github.com/danirod/gradle-propgen-plugin.git'
        developerConnection 'scm:git:https://github.com/danirod/gradle-propgen-plugin.git'
        url 'https://github.com/danirod/gradle-propgen-plugin'
      }

      licenses {
        license {
          name 'The Apache License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
      }

      developers {
        developer {
          id 'danirod'
          name 'Dani Rodr√≠guez'
          email 'danirod@outlook.com'
        }
      }
    }
  }
}
